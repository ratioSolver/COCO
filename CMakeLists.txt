cmake_minimum_required(VERSION 3.21)
project(CoCo VERSION 0.2.0 LANGUAGES CXX)

# Option to enable interactive test mode globally
option(INTERACTIVE_TEST "Enable interactive test mode" OFF)
if(INTERACTIVE_TEST)
    add_compile_definitions(INTERACTIVE_TEST)
endif()

include(CTest)
enable_testing()

set(COCO_NAME "CoCo" CACHE STRING "The CoCo Application Name")

set(CLIPS_INCLUDE_DIR /usr/local/include/clips CACHE PATH "CLIPS include directory")
set(CLIPS_LIB_DIR /usr/local/lib CACHE PATH "CLIPS library directory")

option(BUILD_DELIBERATIVE "Build CoCo deliberative" OFF)
option(BUILD_MONGODB "Build MongoDB connection" OFF)
option(BUILD_FCM "Build Firebase Cloud Messaging" OFF)
option(BUILD_MQTT "Build MQTT middleware" OFF)
option(BUILD_ROS "Build ROS interface" OFF)
option(BUILD_LLM "Build CoCo LLM" OFF)
option(BUILD_COCO_SERVER "Build the CoCo server" OFF)
option(BUILD_COCO_EXECUTABLE "Build the CoCo executable" OFF)

if(BUILD_DELIBERATIVE)
    set(BUILD_MONGODB ON CACHE BOOL "Build MongoDB connection" FORCE)
endif()

if(BUILD_FCM)
    set(BUILD_MONGODB ON CACHE BOOL "Build MongoDB connection" FORCE)
    set(RATIONET_SSL ON CACHE BOOL "Enable SSL" FORCE)
    set(UTILS_ENABLE_CRYPTO ON CACHE BOOL "Build the crypto library" FORCE)
endif()

if(BUILD_MQTT)
    option(MQTT_SECURE "Build MQTT secure connection" OFF)
    option(MQTT_AUTH "Build MQTT authentication" OFF)
    set(MQTT_MAX_BUFFERED_MSGS 100 CACHE STRING "Maximum buffered messages when offline")
endif()

if(BUILD_LLM)
    set(BUILD_MONGODB ON CACHE BOOL "Build MongoDB connection" FORCE)
    set(RATIONET_SSL ON CACHE BOOL "Enable SSL" FORCE)
endif()

if (BUILD_COCO_SERVER)
    option(BUILD_SECURE "Build CoCo secure server (HTTPS)" OFF)
    option(BUILD_AUTH "Build CoCo authentication" OFF)
    option(BUILD_WEB_APP "Build the CoCo web application" OFF)
    option(BUILD_ANDROID "Build the CoCo Android application" OFF)
    set(CLIENT_DIR ${CMAKE_CURRENT_SOURCE_DIR}/gui/app CACHE STRING "Client directory")

    if(BUILD_SECURE)
        set(RATIONET_SSL ON CACHE BOOL "Enable SSL" FORCE)
    endif()

    if(BUILD_AUTH)
        set(BUILD_MONGODB ON CACHE BOOL "Build MongoDB connection" FORCE)
        set(RATIONET_SSL ON CACHE BOOL "Enable SSL" FORCE)
        set(UTILS_ENABLE_CRYPTO ON CACHE BOOL "Build the crypto library" FORCE)
    endif()
endif()

message(STATUS "CoCo name: ${COCO_NAME}")
message(STATUS "Build CoCo deliberative: ${BUILD_DELIBERATIVE}")
message(STATUS "Build MongoDB connection: ${BUILD_MONGODB}")
if(BUILD_MONGODB)
    message(STATUS "Build MongoDB authentication: ${MONGODB_AUTH}")
endif()
message(STATUS "Build CoCo server: ${BUILD_COCO_SERVER}")
message(STATUS "Build Firebase Cloud Messaging: ${BUILD_FCM}")
message(STATUS "Build MQTT middleware: ${BUILD_MQTT}")
if(BUILD_MQTT)
    message(STATUS "Build MQTT secure connection: ${MQTT_SECURE}")
    message(STATUS "Build MQTT authentication: ${MQTT_AUTH}")
    message(STATUS "MQTT maximum buffered messages when offline: ${MQTT_MAX_BUFFERED_MSGS}")
endif()
message(STATUS "Build ROS interface: ${BUILD_ROS}")
message(STATUS "Build CoCo LLM: ${BUILD_LLM}")
if(BUILD_COCO_SERVER)
    message(STATUS "Build secure server (HTTPS): ${BUILD_SECURE}")
    message(STATUS "Build CoCo authentication: ${BUILD_AUTH}")
    if(BUILD_AUTH)
        message(STATUS "Build Firebase Cloud Messaging: ${BUILD_FCM}")
    endif()
    message(STATUS "Build CoCo web application: ${BUILD_WEB_APP}")
    message(STATUS "Client directory: ${CLIENT_DIR}")
    message(STATUS "Build CoCo Android application: ${BUILD_ANDROID}")
endif()

add_library(CoCo src/coco.cpp src/coco_module.cpp src/coco_property.cpp src/coco_type.cpp src/coco_item.cpp  src/coco_rule.cpp src/coco_db.cpp)
target_compile_features(CoCo PUBLIC cxx_std_17)
target_include_directories(CoCo PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include> ${CLIPS_INCLUDE_DIR})
if(NOT TARGET json)
    add_subdirectory(extern/json)
endif()
add_dependencies(CoCo json)
target_link_directories(CoCo PUBLIC ${CLIPS_LIB_DIR})
target_link_libraries(CoCo PUBLIC json clips)
target_compile_definitions(CoCo PUBLIC COCO_NAME="${COCO_NAME}")
setup_sanitizers(CoCo)

if(BUILD_DELIBERATIVE)
    add_subdirectory(extern/plexa)

    target_include_directories(CoCo PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/deliberative>)
    target_sources(CoCo PRIVATE src/deliberative/coco_deliberative.cpp src/deliberative/coco_executor.cpp src/deliberative/deliberative_db.cpp)
    target_compile_definitions(CoCo PUBLIC BUILD_DELIBERATIVE)

    if (BUILD_COCO_SERVER)
        target_sources(CoCo PRIVATE src/deliberative/deliberative_server.cpp)
    endif()

    add_dependencies(CoCo PlExA)
    target_link_libraries(CoCo PUBLIC PlExA)
endif()

if(BUILD_MONGODB)
    find_package(mongocxx REQUIRED)
    find_package(bsoncxx REQUIRED)

    target_sources(CoCo PRIVATE src/db/mongo/mongo_db.cpp)
    target_include_directories(CoCo PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/db/mongo> ${LIBMONGOCXX_INCLUDE_DIR} ${LIBBSONCXX_INCLUDE_DIR})
    target_link_libraries(CoCo PUBLIC mongo::bsoncxx_shared mongo::mongocxx_shared)
    target_compile_definitions(CoCo PUBLIC BUILD_MONGODB)

    if(MONGODB_AUTH)
        target_compile_definitions(CoCo PUBLIC MONGODB_AUTH)
    endif()
endif()

if(BUILD_FCM)
    if(NOT TARGET ratioNet)
        add_subdirectory(extern/rationet)
    endif()

    target_sources(CoCo PRIVATE src/fcm/coco_fcm.cpp src/fcm/fcm_db.cpp)
    target_include_directories(CoCo PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/fcm>)

    if (BUILD_COCO_SERVER)
        target_sources(CoCo PRIVATE src/fcm/fcm_server.cpp)
    endif()

    add_dependencies(CoCo ratioNet)
    target_link_libraries(CoCo PUBLIC ratioNet)
    target_compile_definitions(CoCo PUBLIC BUILD_FCM)
endif()

if(BUILD_MQTT)
    find_package(PahoMqttCpp REQUIRED)

    target_include_directories(CoCo PUBLIC ${PAHO_MQTT_CPP_INCLUDE_DIR} $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/mqtt>)
    target_sources(CoCo PRIVATE src/mqtt/coco_mqtt.cpp)
    target_link_libraries(CoCo PUBLIC PahoMqttCpp::paho-mqttpp3)
    target_compile_definitions(CoCo PUBLIC BUILD_LISTENERS BUILD_MQTT MQTT_MAX_BUFFERED_MSGS=${MQTT_MAX_BUFFERED_MSGS})

    if(MQTT_AUTH)
        target_compile_definitions(CoCo PUBLIC MQTT_AUTH)
    endif()
endif()

if(BUILD_ROS)
    add_subdirectory(ros)
endif()

if(BUILD_LLM)
    if(NOT TARGET ratioNet)
        add_subdirectory(extern/rationet)
    endif()

    target_include_directories(CoCo PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/llm>)
    target_sources(CoCo PRIVATE src/llm/coco_llm.cpp)

    add_dependencies(CoCo ratioNet)
    target_link_libraries(CoCo PUBLIC ratioNet)
    target_compile_definitions(CoCo PUBLIC BUILD_LLM)
endif()

if(BUILD_COCO_SERVER)
    if(NOT TARGET ratioNet)
        add_subdirectory(extern/rationet)
    endif()

    target_sources(CoCo PRIVATE src/server/coco_server.cpp)
    target_include_directories(CoCo PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/server> $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include/auth>)
    add_dependencies(CoCo ratioNet)
    target_link_libraries(CoCo PUBLIC ratioNet)
    target_compile_definitions(CoCo PUBLIC BUILD_LISTENERS BUILD_SERVER CLIENT_DIR="${CLIENT_DIR}")

    if(BUILD_SECURE)
        target_compile_definitions(CoCo PUBLIC BUILD_SECURE)
    endif()

    if(BUILD_AUTH)
        target_sources(CoCo PRIVATE src/auth/coco_auth.cpp src/auth/auth_db.cpp)
        target_compile_definitions(CoCo PUBLIC BUILD_AUTH)
    else()
        target_sources(CoCo PRIVATE src/auth/coco_noauth.cpp)
        target_compile_definitions(CoCo PUBLIC BUILD_NOAUTH)
    endif()

    add_custom_target(npm_coco_install ALL COMMAND npm install WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/gui/coco COMMENT "Installing the CoCo TypeScript library" VERBATIM)
    add_custom_target(npm_coco_build ALL COMMAND npm run build WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/gui/coco COMMENT "Building the CoCo TypeScript library" VERBATIM DEPENDS npm_coco_install)

    if(BUILD_WEB_APP)
        add_custom_target(npm_app_install ALL COMMAND npm install WORKING_DIRECTORY ${CLIENT_DIR} COMMENT "Installing the CoCo web application" VERBATIM DEPENDS npm_coco_build)
        add_custom_target(npm_app_build ALL COMMAND npm run build WORKING_DIRECTORY ${CLIENT_DIR} COMMENT "Building the CoCo web application" VERBATIM DEPENDS npm_app_install)
    endif()

    if(BUILD_ANDROID)
        add_custom_target(mobile_coco_build ALL COMMAND ./gradlew build WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/gui/mobile COMMENT "Building the CoCo mobile application" VERBATIM)
    endif()
endif()

if (BUILD_COCO_EXECUTABLE)
    add_executable(CoCoService src/exec/main.cpp)
    add_dependencies(CoCoService CoCo)
    target_link_libraries(CoCoService PUBLIC CoCo)

    setup_sanitizers(CoCoService)
endif()

if(BUILD_TESTING)
    add_subdirectory(tests)
endif()

set(CPACK_PROJECT_NAME CoCo)
set(CPACK_PROJECT_VERSION ${PROJECT_VERSION})
include(CPack)
